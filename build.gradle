/*
 * Copyright (c) 2016 Duna Project
 * Ministério do Planejamento, Desenvolvimento de Gestão
 * República Federativa do Brasil
 *
 * This file is part of the Duna Project.
 */
plugins {
  id "com.jfrog.bintray" version "1.7"
}

ext.versions = [
  byteBuddy: '1.5.9',
  config: '1.3.1',
  fastClasspathScanner: '2.0.9',
  groovy: '2.4.7',
  guice: '4.1.0',
  ignite: '1.7.0',
  junit: '5.0.0-M2',
  kotlin: '1.0.5-2',
  log4j: '2.7',
  messagePack: '0.8.11',
  mockito: '2.2.28',
  quasar: '0.7.7',
  spock: '1.1-groovy-2.4-rc-3',
  vertx: '3.3.3',
]

configure (allprojects) {
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  group 'io.duna'
  version '0.1.0-SNAPSHOT'

  repositories {
    jcenter()
  }
}

configure (subprojects) {
  apply plugin: 'java'
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'maven-publish'

  apply from: file("$rootDir/gradle/publishing.gradle")

  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

gradle.beforeProject {
  assertCorrectGradleVersion()
}

def assertCorrectGradleVersion() {
  def major = gradle.gradleVersion.split('\\.')[0].toInteger()
  def minor = gradle.gradleVersion.split('\\.')[1].toInteger()

  if (major < 3) {
    throw new ConfigurationCycleException(
      "Gradle 3.1 or greater required to build this project.")
  } else if (major == 3 && minor < 1) {
    throw new ConfigurationCycleException(
      "Gradle 3.1 or greater required to build this project.")
  }
}
