buildscript {
  repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
  }

  dependencies {
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1-M02"
  }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

configurations {
  quasar
}

repositories {
  jcenter()
  maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}

dependencies {
  compile project(':duna-core')
  compile project(':examples:duna-example-api')

  quasar 'co.paralleluniverse:quasar-core:0.7.6:jdk8'
}

classes {
  doFirst {
    ant.taskdef(name: 'scanSuspendables',
        classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
        classpath: "build/classes/main:build/resources/main:${configurations.runtime.asPath}")
    ant.scanSuspendables(auto: false,
        suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
        supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
        append: true) {
      fileset(dir: sourceSets.main.output.classesDir)
    }
  }
}

tasks.withType(JavaExec) {
  jvmArgs "-javaagent:${configurations.quasar.iterator().next()}"
  jvmArgs '-XX:+UseParallelGC'
  jvmArgs '-XX:+UseCondCardMark'

  systemProperty "co.paralleluniverse.fibers.DefaultFiberPool.monitor", "JMX" // "METRICS" // "NONE" //
}

task runApp(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath

  main = 'io.duna.core.Bootstrap'
}