/*
 * Copyright (c) 2016 Duna Project
 * Ministério do Planejamento, Desenvolvimento de Gestão
 * República Federativa do Brasil
 *
 * This file is part of the Duna Project.
 */
plugins {
  id "nebula.kotlin" version "1.0.5-2"
}

apply plugin: 'kotlin'

sourceSets {
  echo
  caller
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  compile project(':duna-core')

  callerCompile sourceSets.main.runtimeClasspath
  callerCompile sourceSets.echo.output
  callerCompile "co.paralleluniverse:quasar-kotlin:0.7.7"

  echoCompile sourceSets.main.runtimeClasspath

  testCompile "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
  testCompile "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
  testCompile "org.mockito:mockito-core:${versions.mockito}"
}

classes {
  doFirst {
    ant.taskdef(name: 'scanSuspendables',
      classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
      classpath: "build/classes/main:build/resources/main:${configurations.runtime.asPath}")
    ant.scanSuspendables(auto: false,
      suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
      supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
      append: true) {
      fileset(dir: sourceSets.main.output.classesDir)
    }
  }
}

task apiJar(type: Jar) {
  from sourceSets.main.runtimeClasspath
  exclude "**/impl"
}
