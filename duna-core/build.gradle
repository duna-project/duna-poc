buildscript {
  repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
  }

  dependencies {
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1-M02"
  }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

configurations {
  quasar
}

ext {
  byteBuddyVersion = "1.5.7"
  asmVersion = "5.1"
}

repositories {
  jcenter()
  maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}

dependencies {
  compile 'org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1-M02'

  compile project(':duna-annotations')

  compile 'io.vertx:vertx-core:3.3.3'
  compile 'io.vertx:vertx-sync:3.3.3'
  compile 'io.vertx:vertx-ignite:3.3.3'

  compile 'co.paralleluniverse:quasar-core:0.7.6:jdk8'
  compile 'co.paralleluniverse:quasar-kotlin:0.7.6'

  compile 'com.google.inject:guice:4.1.0'
  compile 'com.google.inject.extensions:guice-multibindings:4.1.0'

  compile "net.bytebuddy:byte-buddy:${byteBuddyVersion}"

  compile 'org.apache.logging.log4j:log4j-api:2.6.2'
  compile 'org.apache.logging.log4j:log4j-core:2.6.2'

  compile 'org.msgpack:msgpack-core:0.8.10'
  compile 'org.msgpack:jackson-dataformat-msgpack:0.8.10'

  compile 'io.github.lukehutch:fast-classpath-scanner:2.0.3'

  compile 'org.apache.commons:commons-lang3:3.5'

  compile 'com.typesafe:config:1.3.1'

  testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M2'
  testCompile 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M2'
  testCompile 'org.mockito:mockito-core:2.+'

  quasar 'co.paralleluniverse:quasar-core:0.7.6:jdk8'
}

classes {
  doFirst {
    ant.taskdef(name: 'scanSuspendables',
        classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
        classpath: "build/classes/main:build/resources/main:${configurations.runtime.asPath}")
    ant.scanSuspendables(auto: false,
        suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
        supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
        append: true) {
      fileset(dir: sourceSets.main.output.classesDir)
    }
  }
}

tasks.withType(JavaExec) {
  jvmArgs "-javaagent:${configurations.quasar.iterator().next()}"
  jvmArgs '-XX:+UseParallelGC'
  jvmArgs '-XX:+UseCondCardMark'

  systemProperty "co.paralleluniverse.fibers.DefaultFiberPool.monitor", "JMX" // "METRICS" // "NONE" //
}
