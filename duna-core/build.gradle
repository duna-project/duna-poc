buildscript {
  repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
  }

  dependencies {
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}"

  compile project(':duna-annotations')
  compile project(':duna-agent')

  compile "io.vertx:vertx-core:${versions.vertx}"
  compile "io.vertx:vertx-sync:${versions.vertx}"
  compile "io.vertx:vertx-ignite:${versions.vertx}", {
    exclude module: 'ignite-spring'
  }

  compile "co.paralleluniverse:quasar-core:${versions.quasar}:jdk8"

  compile "com.google.inject:guice:${versions.guice}"
  compile "com.google.inject.extensions:guice-multibindings:${versions.guice}"

  compile "net.bytebuddy:byte-buddy:${versions.byteBuddy}"
  compile "net.bytebuddy:byte-buddy-agent:${versions.byteBuddy}"

  compile "org.apache.ignite:ignite-log4j2:${versions.ignite}"
  compile "org.apache.logging.log4j:log4j-core:${versions.log4j}"
  compile "org.apache.logging.log4j:log4j-jul:${versions.log4j}"

  compile "org.msgpack:msgpack-core:${versions.messagePack}"
  compile "org.msgpack:jackson-dataformat-msgpack:${versions.messagePack}"

  compile "io.github.lukehutch:fast-classpath-scanner:${versions.fastClasspathScanner}"

  compile "com.ea.agentloader:ea-agent-loader:1.0.2"

  compile "com.typesafe:config:${versions.config}"

  testCompile "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
  testCompile "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
  testCompile "org.mockito:mockito-core:${versions.mockito}"
}

classes {
  doFirst {
    ant.taskdef(name: 'scanSuspendables',
        classname: 'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
        classpath: "build/classes/main:build/resources/main:${configurations.runtime.asPath}")
    ant.scanSuspendables(auto: false,
        suspendablesFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendables",
        supersFile: "$sourceSets.main.output.resourcesDir/META-INF/suspendable-supers",
        append: true) {
      fileset(dir: sourceSets.main.output.classesDir)
    }
  }
}
