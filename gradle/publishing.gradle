/*
 * Copyright (c) 2016 Duna Project
 * Ministério do Planejamento, Desenvolvimento de Gestão
 * República Federativa do Brasil
 *
 * This file is part of the Duna Project.
 */
def bintrayRepo = project.version.endsWith("-SNAPSHOT") ?
  'duna-snapshots' : 'duna'

bintray {
  user = bintrayUser
  key = bintrayApiKey

  pkg {
    repo = bintrayRepo
    name = project.name
    userOrg = 'duna-project'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/cemelo/duna.git'

    version {
      name = project.version
      released  = new Date()
    }

    publications = ['Duna']
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from project.sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    Duna(MavenPublication) {
      from components.java

      pom.withXml {
        asNode().appendNode('url', 'http://duna.io')
      }
    }
  }
}
